defmodule Podium.Chart do
  @moduledoc """
  Chart struct and XML generation for embedding charts in slides.

  A chart combines a chart type, chart data, and formatting options (title,
  legend, axes, data labels). The chart XML is generated by `Podium.Chart.XmlWriter`
  and the embedded Excel workbook by `Podium.Chart.XlsxWriter`.
  """

  alias Podium.OPC.Constants
  alias Podium.Units

  defstruct [
    :chart_type,
    :chart_data,
    :chart_index,
    :x,
    :y,
    :width,
    :height,
    :title,
    :legend,
    :combo,
    data_labels: [],
    category_axis: [],
    value_axis: [],
    secondary_value_axis: []
  ]

  @type t :: %__MODULE__{
          chart_type: Podium.chart_type() | :combo,
          chart_data: struct(),
          chart_index: pos_integer() | nil,
          x: non_neg_integer(),
          y: non_neg_integer(),
          width: non_neg_integer(),
          height: non_neg_integer(),
          title: String.t() | keyword() | nil,
          legend: atom() | keyword() | false | nil,
          combo: Podium.Chart.ComboChart.t() | nil,
          data_labels: keyword() | [atom()],
          category_axis: keyword(),
          value_axis: keyword(),
          secondary_value_axis: keyword()
        }

  @doc """
  Creates a new chart.
  """
  @spec new(atom(), struct(), keyword()) :: t()
  def new(chart_type, chart_data, opts) do
    %__MODULE__{
      chart_type: chart_type,
      chart_data: chart_data,
      chart_index: nil,
      x: Units.to_emu(Keyword.fetch!(opts, :x)),
      y: Units.to_emu(Keyword.fetch!(opts, :y)),
      width: Units.to_emu(Keyword.fetch!(opts, :width)),
      height: Units.to_emu(Keyword.fetch!(opts, :height)),
      title: Keyword.get(opts, :title),
      legend: Keyword.get(opts, :legend),
      data_labels: Keyword.get(opts, :data_labels, []),
      category_axis: Keyword.get(opts, :category_axis, []),
      value_axis: Keyword.get(opts, :value_axis, [])
    }
  end

  @doc """
  Creates a new combo chart.
  """
  @spec new_combo(Podium.Chart.ComboChart.t(), keyword()) :: t()
  def new_combo(%Podium.Chart.ComboChart{} = combo, opts) do
    %__MODULE__{
      chart_type: :combo,
      chart_data: combo.chart_data,
      chart_index: nil,
      combo: combo,
      x: Units.to_emu(Keyword.fetch!(opts, :x)),
      y: Units.to_emu(Keyword.fetch!(opts, :y)),
      width: Units.to_emu(Keyword.fetch!(opts, :width)),
      height: Units.to_emu(Keyword.fetch!(opts, :height)),
      title: Keyword.get(opts, :title),
      legend: Keyword.get(opts, :legend),
      data_labels: Keyword.get(opts, :data_labels, []),
      category_axis: Keyword.get(opts, :category_axis, []),
      value_axis: Keyword.get(opts, :value_axis, []),
      secondary_value_axis: Keyword.get(opts, :secondary_value_axis, [])
    }
  end

  @doc """
  Returns the partname for the chart XML.
  """
  @spec partname(t()) :: String.t()
  def partname(%__MODULE__{chart_index: idx}), do: "ppt/charts/chart#{idx}.xml"

  @doc """
  Returns the rels partname for this chart.
  """
  @spec rels_partname(t()) :: String.t()
  def rels_partname(%__MODULE__{chart_index: idx}),
    do: "ppt/charts/_rels/chart#{idx}.xml.rels"

  @doc """
  Returns the partname for the embedded Excel workbook.
  """
  @spec xlsx_partname(t()) :: String.t()
  def xlsx_partname(%__MODULE__{chart_index: idx}),
    do: "ppt/embeddings/Microsoft_Excel_Sheet#{idx}.xlsx"

  @doc """
  Generates the graphic frame XML for embedding the chart in a slide.
  The `shape_id` is the shape ID within the slide, and `r_id` is the
  relationship ID linking to the chart part.
  """
  @spec graphic_frame_xml(t(), pos_integer(), String.t()) :: String.t()
  def graphic_frame_xml(%__MODULE__{} = chart, shape_id, r_id) do
    ns_a = Constants.ns(:a)
    ns_p = Constants.ns(:p)
    ns_r = Constants.ns(:r)
    ns_c = Constants.ns(:c)

    ~s(<p:graphicFrame xmlns:a="#{ns_a}" xmlns:p="#{ns_p}" xmlns:r="#{ns_r}">) <>
      ~s(<p:nvGraphicFramePr>) <>
      ~s(<p:cNvPr id="#{shape_id}" name="Chart #{chart.chart_index}"/>) <>
      ~s(<p:cNvGraphicFramePr><a:graphicFrameLocks noGrp="1"/></p:cNvGraphicFramePr>) <>
      ~s(<p:nvPr/>) <>
      ~s(</p:nvGraphicFramePr>) <>
      ~s(<p:xfrm>) <>
      ~s(<a:off x="#{chart.x}" y="#{chart.y}"/>) <>
      ~s(<a:ext cx="#{chart.width}" cy="#{chart.height}"/>) <>
      ~s(</p:xfrm>) <>
      ~s(<a:graphic>) <>
      ~s(<a:graphicData uri="#{ns_c}">) <>
      ~s(<c:chart xmlns:c="#{ns_c}" r:id="#{r_id}"/>) <>
      ~s(</a:graphicData>) <>
      ~s(</a:graphic>) <>
      ~s(</p:graphicFrame>)
  end
end
